apply plugin: 'com.android.library'
apply plugin: 'robolectric'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }

    lintOptions {
        // This seems to be firing due to okio referencing java.nio.File
        // which is harmless for us. Not sure how to disable this in
        // more targeted fashion...
        warning 'InvalidPackage'
    }
}

robolectric {
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'
}

dependencies {
    compile project(':stetho')
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    //noinspection GradleDynamicVersion
    compile 'com.squareup.okhttp:okhttp:2.2.0+'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
    androidTestCompile 'org.powermock:powermock-api-mockito:1.6.1'
    androidTestCompile 'org.powermock:powermock-module-junit4:1.6.1'

    // Needed for Robolectric and PowerMock to be combined in a single test.
    androidTestCompile 'org.powermock:powermock-module-junit4-rule:1.6.1'
    androidTestCompile 'org.powermock:powermock-classloading-xstream:1.6.1'

    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.2.0'
}

apply from: rootProject.file('release.gradle')
